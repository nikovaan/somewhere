////////////////////
// Player changes //
////////////////////

ACTOR boltDoomPlayer : DoomPlayer replaces DoomPlayer
{
	Player.StartItem "boltPistol"
    Player.StartItem "boltFist"
	Player.StartItem "Clip", 50
	Player.WeaponSlot 1, boltFist, boltChainsaw
	Player.WeaponSlot 2, boltPistol
	Player.WeaponSlot 3, boltShotgun, boltSuperShotgun
	Player.WeaponSlot 4, boltChaingun
	Player.WeaponSlot 5, boltRocketLauncher
	Player.WeaponSlot 6, boltPlasmaRifle
	Player.WeaponSlot 7, boltBFG9000
	States
	{
		Spawn:
			PLAY A -1
			Loop
		See:
			PLAY ABCD 4
			Loop
		Missile:
			PLAY E 12
			Goto Spawn
		Melee:
			PLAY F 6 BRIGHT
			Goto Missile
		Pain:
			PLAY G 4
			PLAY G 4 A_Pain
			Goto Spawn
		Death:
			PLAY H 0 A_PlayerSkinCheck("AltSkinDeath")
		Death1:
			PLAY H 10
			PLAY I 10 A_PlayerScream
			PLAY J 10 A_NoBlocking
			PLAY KLM 10
			PLAY N -1
			Stop
		XDeath:
			PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath")
		XDeath1:
			PLAY O 5
			PLAY P 5 A_XScream
			PLAY Q 5 A_NoBlocking
			PLAY RSTUV 5
			PLAY W -1
			Stop
		AltSkinDeath:
			PLAY H 6
			PLAY I 6 A_PlayerScream
			PLAY JK 6
			PLAY L 6 A_NoBlocking
			PLAY MNO 6
			PLAY P -1
			Stop
		AltSkinXDeath:
			PLAY Q 5 A_PlayerScream
			PLAY R 0 A_NoBlocking
			PLAY R 5 A_SkullPop
			PLAY STUVWX 5
			PLAY Y -1
			Stop
	}
}

/////////////////////
// Powerup changes //
/////////////////////

ACTOR boltBerserk : CustomInventory replaces Berserk
{
	States
	{
	    Spawn:
		    PSTR A -1
			Stop
	    Pickup:
	        TNT1 A 0 A_GiveInventory("PowerStrength")
			TNT1 A 0 HealThing (100, 0)
			TNT1 A 0 A_SelectWeapon("boltFist")
			Stop
	}
}

ACTOR boltBlursphere : Blursphere replaces Blursphere
{
    +INVENTORY.ADDITIVETIME
	Powerup.Type "Ghost"
}

ACTOR boltRadSuit : PowerupGiver replaces RadSuit 2025
{
    Game Doom
	Height 46
	SpawnID 46
	Inventory.MaxAmount 0
	Inventory.PickupMessage "$GOTSUIT" // "Radiation Shielding Suit"
	//Inventory.PickupSound  TODO pickup sound for radsuit
	Powerup.Duration -60
	Powerup.Color 0, 255, 0, 0.125
    +INVENTORY.ADDITIVETIME
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Powerup.Type "boltProtection"
	//Powerup.Type "IronFeet"
	States
	{
	    Spawn:
		    SUIT A -1 Bright
			Stop
	}
}

ACTOR PowerboltProtection : PowerProtection
{
    Powerup.Duration -60
    +INVENTORY.ADDITIVETIME
	+NORADIUSDMG
	DamageFactor "Slime", 0.0
}

ACTOR boltInvulnerabilitySphere : InvulnerabilitySphere replaces InvulnerabilitySphere
{
    +INVENTORY.ADDITIVETIME
	Powerup.Color GoldMap
}

ACTOR boltInfrared : Infrared replaces Infrared
{
    +INVENTORY.ADDITIVETIME
}

////////////////////
// Weapon changes //
////////////////////

ACTOR boltFist : Fist replaces Fist
{
	-WIMPY_WEAPON
    States
	{
	    Ready:
		    PUNG A 1 A_WeaponReady
			Loop
		Deselect:
		    PUNG A 0 A_Lower
			Loop
		Select:
		    PUNG A 0 A_Raise
			Loop
	    Fire:
		    PUNG B 4
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
			PUNG C 4 A_CustomPunch(20, TRUE, 0, "BulletPuff", 80, 0)
			Goto FireEnd
		Berserked:
		    PUNG C 4 A_CustomPunch(141, TRUE, 0, "BulletPuff", 80, 0.1)
			Goto FireEnd
		FireEnd:
		    PUNG D 4
			PUNG C 4
			PUNG B 5 A_ReFire
			Goto Ready
	}
}

ACTOR boltChainsaw : Chainsaw replaces Chainsaw
{
    States
	{
	    Ready:
		    SAWG CD 4 A_WeaponReady
			Loop
		Deselect:
		    SAWG C 0 A_Lower
		    Loop
		Select:
		    SAWG C 0 A_Raise
			Loop
	    Fire:
		    SAWG AB 1 A_Saw
			SAWG B 0 A_ReFire
			Goto Ready
		Spawn:
		    CSAW A -1
		    Stop
	}
}

ACTOR boltPistol : Pistol replaces Pistol
{
    -WIMPY_WEAPON
    States
	{
	    Ready:
		    PISG A 1 A_WeaponReady
			Loop
		Deselect:
		    PISG A 0 A_Lower
			Loop
		Select:
		    PISG A 0 A_Raise
			Loop
	    Fire:
		    PISG A 3
			PISG B 0 A_FireBullets(0, 0, 1, 10, "BulletPuff", FBF_EXPLICITANGLE|FBF_USEAMMO|FBF_NORANDOM)
			PISG B 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
			PISG B 5 A_GunFlash
			PISG C 3
			PISG B 4 A_ReFire
			Goto Ready
		Flash:
		    PISF A 7 Bright A_Light1
			Goto LightDone
			PISF A 7 Bright A_Light1
			Goto LightDone
		Spawn:
		    PIST A -1
			Stop
	}
}

ACTOR boltShotgun : Shotgun replaces Shotgun
{
    States
	{
	    Ready:
		    SHTG A 1 A_WeaponReady
			Loop
		Deselect:
		    SHTG A 0 A_Lower
			Loop
		Select:
		    SHTG A 0 A_Raise
			Loop
	    Fire:
	        SHTG A 3
		    SHTG A 0 A_FireBullets(3, 0, 9, 5, "BulletPuff")
    		SHTG A 0 A_PlaySound("weapons/shotgf", CHAN_WEAPON)
		    SHTG A 7 A_GunFlash
		    SHTG BC 5
		    SHTG D 4
		    SHTG CB 5
		    SHTG A 3
		    SHTG A 7 A_ReFire
		    Goto Ready
		Flash:
		    SHTF A 4 Bright A_Light1
			SHTF B 3 Bright A_Light2
			Goto LightDone
		Spawn:
		    SHOT A -1
			Stop
	}
}

ACTOR boltSuperShotgun : SuperShotgun replaces SuperShotgun
{
    States
	{
	    Ready:
		    SHT2 A 1 A_WeaponReady
			Loop
		Deselect:
		    SHT2 A 0 A_Lower
			Loop
		Select:
		     SHT2 A 0 A_Raise
			 Loop
	    Fire:
		    SHT2 A 3
		    SHT2 A 0 A_FireBullets(11.2, 7.1, 21, 5, "BulletPuff")
			SHT2 A 0 A_PlaySound("weapon/sshotf", CHAN_WEAPON)
			SHT2 A 7 A_GunFlash
			SHT2 B 7
			SHT2 C 7 A_CheckReload
			SHT2 D 7 A_OpenShotgun2
			SHT2 E 7
			SHT2 F 7 A_LoadShotgun2
			SHT2 G 6
			SHT2 H 6 A_CloseShotgun2
			SHT2 A 5 A_ReFire
			Goto Ready
		Flash:
		    SHT2 I 4 Bright A_Light1
			SHT2 J 3 Bright A_Light2
			Goto LightDone
		Spawn:
		    SGN2 A -1
			Stop
	}
}

ACTOR boltChaingun : Chaingun replaces Chaingun
{
    States
	{
	    Ready:
		    CHGG A 1 A_WeaponReady
		    Loop
		Deselect:
		    CHGG A 0 A_Lower
			Loop
		Select:
		    CHGG A 0 A_Raise
			Loop
	    Fire:
		    CHGG AB 3 A_FireCGun
			CHGG B 0 A_ReFire
			Goto Ready
		Flash:
		    CHGF A 5 Bright A_Light1
			Goto LightDone
			CHGF B 5 Bright A_Light1
			Goto LightDone
		Spawn:
		    MGUN A -1
			Stop
	}
}

ACTOR boltRocketLauncher : RocketLauncher replaces RocketLauncher
{
    States
	{
	    Ready:
		    MISG A 1 A_WeaponReady
			Loop
		Deselect:
		    MISG A 0 A_Lower
			Loop
		Select:
		    MISG A 0 A_Raise
			Loop
	    Fire:
		    MISG B 8 A_GunFlash
			MISG B 12 A_FireCustomMissile("boltRocket")
			MISG B 0 A_ReFire
			Goto Ready
		Flash:
		    MISF A 3 Bright A_Light1
			MISF B 4 Bright
			MISF CD 4 Bright A_Light2
			Goto LightDone
		Spawn:
		    LAUN A -1
			Stop
	}
}

ACTOR boltRocket : Rocket replaces Rocket
{
    Speed 40
	Damage (random(3, 6) * 20)
}

ACTOR boltPlasmaRifle : PlasmaRifle replaces PlasmaRifle
{
    States
	{
	    Ready:
		    PLSG A 1 A_WeaponReady
			Loop
		Deselect:
		    PLSG A 0 A_Lower
			Loop
		Select:
		    PLSG A 0 A_Raise
			Loop
	    Fire:
		    PLSG A 3 A_FirePlasma
			PLSG B 20 A_ReFire
			Goto Ready
		Flash:
		    PLSF A 4 Bright A_Light1
			Goto LightDone
			PLSF B 4 Bright A_Light1
			Goto Lightdone
		Spawn:
		    PLAS A -1
			Stop
	}
}

ACTOR boltPlasmaBall : PlasmaBall replaces PlasmaBall
{
    Speed 40
}

ACTOR boltBFG9000 : BFG9000 replaces BFG9000
{
    States
	{
	    Ready:
		    BFGG A 1 A_WeaponReady
			Loop
		Deselect:
		    BFGG A 0 A_Lower
			Loop
		Select:
		    BFGG A 0 A_Raise
			Loop
	    Fire:
		    BFGG A 20 A_BFGSound
			BFGG B 10 A_GunFlash
			BFGG B 10 A_FireBFG
			BFGG B 20 A_ReFire
			Goto Ready
		Flash:
		    BFGF A 11 Bright A_Light1
			BFGF B 6 Bright A_Light2
			Goto LightDone
		Spawn:
		    BFUG A -1
			Stop
		OldFire:
		    BFGG A 10 A_BFGSound
			BFGG BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_FireOldBFG
			BFGG B 0 A_Light0
			BFGG B 20 A_ReFire
			Goto Ready
	}
}

/////////////////////
// Monster changes //
/////////////////////

ACTOR boltZombieMan : Zombieman replaces ZombieMan
{
    States
    {
        Spawn:
            POSS AB 10 A_Look
            Loop
        See:
            POSS AABBCCDD 4 A_Chase
            Loop
        Missile:
            POSS E 10 A_FaceTarget
			POSS E 0 A_PlaySound ("grunt/attack")
            POSS F 8 A_CustomBulletAttack (22.5, 0, 1, (1 * random(1,4) + 5), "BulletPuff", 0, CBAF_NORANDOM)
            POSS E 8
            Goto See
        Pain:
            POSS G 3
            POSS G 3 A_Pain
            Goto See
        Death:
            POSS H 5
            POSS I 5 A_Scream
            POSS J 5 A_NoBlocking
            POSS K 5
            POSS L -1
            Stop
        XDeath:
            POSS M 5
            POSS N 5 A_XScream
            POSS O 5 A_NoBlocking
            POSS PQRST 5
            POSS U -1
            Stop
        Raise:
            POSS K 5
            POSS JIH 5
            Goto See
    }
}

ACTOR boltShotgunGuy : ShotgunGuy replaces ShotgunGuy
{
    DropItem "boltShotgun"
	States
	{
		Spawn:
			SPOS AB 10 A_Look
			Loop
		See:
			SPOS AABBCCDD 3 A_Chase
			Loop
		Missile:
			SPOS E 10 A_FaceTarget
			SPOS F 0 Bright A_PlaySound("shotguy/attack", CHAN_WEAPON)
			SPOS F 10 Bright A_CustomBulletAttack(22.5, 0, 3,  (1 * random(1,4) + 5), "BulletPuff", 0, CBAF_NORANDOM)
			SPOS E 10
			Goto See
		Pain:
			SPOS G 3
			SPOS G 3 A_Pain
			Goto See
		Death:
			SPOS H 5
			SPOS I 5 A_Scream
			SPOS J 5 A_NoBlocking
			SPOS K 5
			SPOS L -1
			Stop
		XDeath:
			SPOS M 5
			SPOS N 5 A_XScream
			SPOS O 5 A_NoBlocking
			SPOS PQRST 5
			SPOS U -1
			Stop
		Raise:
			SPOS L 5
			SPOS KJIH 5
			Goto See
	}
}

ACTOR boltChaingunGuy : ChaingunGuy replaces ChaingunGuy
{
    DropItem "boltChaingun"
	States
	{
		Spawn:
			CPOS AB 10 A_Look
			Loop
		See:
			CPOS AABBCCDD 3 A_Chase
			Loop
		Missile:
			CPOS E 10 A_FaceTarget
			CPOS F 0 Bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			CPOS F 4 Bright A_CustomBulletAttack(22.5, 0, 1, (1 * random(1,4) + 5), "BulletPuff", 0, CBAF_NORANDOM)
			CPOS E 0 Bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			CPOS E 4 Bright A_CustomBulletAttack(22.5, 0, 1, (1 * random(1,4) + 5), "BulletPuff", 0, CBAF_NORANDOM)
			CPOS F 1 A_CPosRefire
			Goto Missile+1
		Pain:
			CPOS G 3
			CPOS G 3 A_Pain
			Goto See
		Death:
			CPOS H 5
			CPOS I 5 A_Scream
			CPOS J 5 A_NoBlocking
			CPOS KLM 5
			CPOS N -1
			Stop
		XDeath:
			CPOS O 5
			CPOS P 5 A_XScream
			CPOS Q 5 A_NoBlocking
			CPOS RS 5
			CPOS T -1
			Stop
        Raise:
			CPOS N 5
			CPOS MLKJIH 5
            Goto See
	}
}

ACTOR boltDemon : Demon replaces Demon
{
    Health 140
    DropItem "HealthBonus"
	States
	{
		Spawn:
			SARG AB 10 A_Look
			Loop
		See:
			SARG AABBCCDD 2 Fast A_Chase
			Loop
		Melee:
			SARG EF 6 Fast A_FaceTarget
			SARG G 6 Fast A_SargAttack
			Goto See
		Pain:
			SARG H 2 Fast
			SARG H 2 Fast A_Pain
			Goto See
		Death:
			SARG I 8
			SARG J 8 A_Scream
			SARG K 4
			SARG L 4 A_NoBlocking
			SARG M 4
			SARG N -1
			Stop
		Raise:
			SARG N 5
			SARG MLKJI 5
			Goto See
	}
}

ACTOR boltSpectre : boltDemon replaces Spectre 58
{
	Game Doom
	SpawnID 9
    Health 140
    DropItem "HealthBonus"
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
	SeeSound "spectre/sight"
	AttackSound "spectre/melee"
	PainSound "spectre/pain"
	DeathSound "spectre/death"
	ActiveSound "spectre/active"
	HitObituary "$OB_SPECTREHIT" // "%o was eaten by a spectre."
}

ACTOR boltDoomImp : DoomImp replaces DoomImp
{
    DropItem "ArmorBonus"
	States
	{
		Spawn:
			TROO AB 10 A_Look
			Loop
		See:
			TROO AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			TROO EF 8 A_FaceTarget
			TROO G 6 A_CustomComboAttack("boltDoomImpBall", 32, (3 * random(1, 3) + 6), "imp/melee")
			Goto See
		Pain:
			TROO H 2
			TROO H 2 A_Pain
			Goto See
		Death:
			TROO I 8
			TROO J 8 A_Scream
			TROO K 6
			TROO L 6 A_NoBlocking
			TROO M -1
			Stop
		XDeath:
			TROO N 5
			TROO O 5 A_XScream
			TROO P 5
			TROO Q 5 A_NoBlocking
			TROO RST 5
			TROO U -1
			Stop
		Raise:
			TROO MLKJI 8
			Goto See
	}
}

ACTOR boltDoomImpBall : DoomImpBall replaces DoomImpBall
{
	Damage (3 * random(1, 3) + 6)
}

ACTOR boltRevenant : Revenant replaces Revenant
{
    Height 64
    DropItem "RocketAmmo"
	States
	{
		Spawn:
			SKEL AB 10 A_Look
			Loop
		See:
			SKEL AABBCCDDEEFF 2 A_Chase
			Loop
		Melee:
			SKEL G 1 A_FaceTarget
			SKEL G 6 A_SkelWhoosh
			SKEL H 6 A_FaceTarget
			SKEL I 6 A_CustomMeleeAttack((1 * random(0, 6) + 30),"skeleton/melee","none")
			Goto See
		Missile:
			SKEL J 1 Bright A_FaceTarget
			SKEL J 9 Bright A_FaceTarget
			SKEL K 10 A_SkelMissile
			SKEL K 10 A_FaceTarget
			Goto See
		Pain:
			SKEL L 5
			SKEL L 5 A_Pain
			Goto See
		Death:
			SKEL LM 7
			SKEL N 7 A_Scream
			SKEL O 7 A_NoBlocking
			SKEL P 7
			SKEL Q -1
			Stop
		Raise:
			SKEL Q 5
			SKEL PONML 5
			Goto See
	}
}

ACTOR boltRevenantTracer : RevenantTracer replaces RevenantTracer
{
    Damage (1 * random(0, 8) + 32)
    +DONTSEEKINVISIBLE
}

ACTOR boltFatso : Fatso replaces Fatso
{
    DropItem "HealthBonus"
	States
	{
		Spawn:
			FATT AB 15 A_Look
			Loop
		See:
			FATT AABBCCDDEEFF 4 A_Chase
			Loop
		Missile:
			FATT G 20 A_FatRaise
			FATT H 10 Bright A_FatAttack1
			FATT IG 5
			FATT H 10 Bright A_FatAttack2
			FATT IG 5
			FATT H 10 Bright A_FatAttack3
			FATT IG 5
			Goto See
		Pain:
			FATT J 3
			FATT J 3 A_Pain
			Goto See
		Death:
			FATT K 6
			FATT L 6 A_Scream
			FATT M 6 A_NoBlocking
			FATT NOPQRS 6
			FATT T -1 A_BossDeath
			Stop
		Raise:
			FATT R 5
			FATT QPONMLK 5
			Goto See
	}
}

ACTOR boltFatShot : FatShot replaces FatShot
{
	    Damage (1 * random(0, 8) + 32)
}

ACTOR boltLostSoul : LostSoul replaces LostSoul
{
    Health 35
    DropItem "ArmorBonus"
	Damage (3 * random(1, 3) + 6)
	States
	{
		Spawn:
			SKUL AB 10 Bright A_Look
			Loop
		See:
			SKUL AB 6 Bright A_Chase
			Loop
		Missile:
			SKUL C 10 Bright A_FaceTarget
			SKUL D 4 Bright A_SkullAttack
			SKUL CD 4 Bright
			Goto Missile+2
		Pain:
			SKUL E 3 Bright
			SKUL E 3 Bright A_Pain
			Goto See
		Death:
			SKUL F 6 Bright
			SKUL G 6 Bright A_Scream
			SKUL H 6 Bright
			SKUL I 6 Bright A_NoBlocking
			SKUL J 6
			SKUL K 6
			Stop
	}
}

ACTOR boltPainElemental : PainElemental replaces PainElemental
{}

ACTOR boltCacodemon : Cacodemon replaces Cacodemon
{
    DropItem "HealthBonus"
	States
	{
		Spawn:
			HEAD A 10 A_Look
			Loop
		See:
			HEAD A 3 A_Chase
			Loop
		Missile:
			HEAD BC 5 A_FaceTarget
			HEAD D 5 Bright A_CustomComboAttack("boltCacodemonBall", 32, (1 * random(0, 10) + 30))
			Goto See
		Pain:
			HEAD E 3
			HEAD E 3 A_Pain
			HEAD F 6
			Goto See
		Death:
			HEAD G 8
			HEAD H 8 A_Scream
			HEAD IJ 8
			HEAD K 8 A_NoBlocking
			HEAD L -1 A_SetFloorClip
			Stop
		Raise:
			HEAD L 8 A_UnSetFloorClip
			HEAD KJIHG 8
			Goto See
	}
}

ACTOR boltCacodemonBall : CacodemonBall replaces CacodemonBall
{
	Damage (1 * random(0, 5) + 20)
}

ACTOR boltHellKnight : HellKnight replaces HellKnight
{
    DropItem "ArmorBonus"
	States
	{
		Spawn:
			BOS2 AB 10 A_Look
			Loop
		See:
			BOS2 AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			BOS2 EF 8 A_FaceTarget
			BOS2 G 8 A_CustomComboAttack("boltBaronBall", 32, (1 * random(0, 10) + 40), "baron/melee")
			Goto See
		Pain:
			BOS2 H 2
			BOS2 H 2 A_Pain
			Goto See
		Death:
			BOS2 I 8
			BOS2 J 8 A_Scream
			BOS2 K 8
			BOS2 L 8 A_NoBlocking
			BOS2 MN 8
			BOS2 O -1
			Stop
		Raise:
			BOS2 O 8
			BOS2 NMLKJI 8
			Goto See
	}
}

ACTOR boltBaronBall : BaronBall replaces BaronBall
{
	Damage (1 * random(0, 10) + 40)
}

ACTOR boltBaronOfHell : BaronOfHell replaces BaronOfHell
{
    DropItem "Stimpack"
	States
	{
		Spawn:
			BOSS AB 10 A_Look
			Loop
		See:
			BOSS AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			BOSS EF 8 A_FaceTarget
			BOSS G 8 A_CustomComboAttack("boltBaronBall", 32, (1 * random(0, 10) + 40), "baron/melee")
			Goto See
		Pain:
			BOSS H 2
			BOSS H 2 A_Pain
			Goto See
		Death:
			BOSS I 8
			BOSS J 8 A_Scream
			BOSS K 8
			BOSS L 8 A_NoBlocking
			BOSS MN 8
			BOSS O -1 A_BossDeath
			Stop
		Raise:
			BOSS O 8
			BOSS NMLKJI 8
			Goto See
	}
}

ACTOR boltArchvile : Archvile replaces Archvile
{
    DropItem "Stimpack"
	States
	{
		Spawn:
			VILE AB 10 A_Look
			Loop
		See:
			VILE AABBCCDDEEFF 2 A_VileChase
			Loop
		Missile:
			VILE G 0 Bright A_VileStart
			VILE G 0 Bright A_Changeflag("QUICKTORETALIATE", 0)
			VILE G 10 Bright A_FaceTarget
			VILE H 8 Bright A_VileTarget
			VILE IJKLMN 8 Bright A_FaceTarget
			VILE O 8 Bright A_VileAttack
			VILE P 20 Bright A_Changeflag("QUICKTORETALIATE", 1)
			Goto See
		Heal:
			VILE "[\]" 10 Bright
			Goto See
		Pain:
			VILE Q 5
			VILE Q 5 A_Pain
			Goto See
		Death:
			VILE Q 7
			VILE R 7 A_Scream
			VILE S 7 A_NoBlocking
			VILE TUVWXY 7
			VILE Z -1
			Stop
	}
}

ACTOR boltArachnotron : Arachnotron replaces Arachnotron
{
    DropItem "Cell"
	States
	{
		Spawn:
			BSPI AB 10 A_Look
			Loop
		See:
			BSPI A 20
			BSPI A 3 A_BabyMetal
			BSPI ABBCC 3 A_Chase
			BSPI D 3 A_BabyMetal
			BSPI DEEFF 3 A_Chase
			Goto See+1
		Missile:
			BSPI A 20 Bright A_FaceTarget
			BSPI G 4 Bright A_BspiAttack
			BSPI H 4 Bright
			BSPI H 1 Bright A_SpidRefire
			Goto Missile+1
		Pain:
			BSPI I 3
			BSPI I 3 A_Pain
			Goto See+1
		Death:
			BSPI J 20 A_Scream
			BSPI K 7 A_NoBlocking
			BSPI LMNO 7
			BSPI P -1 A_BossDeath
			Stop
		Raise:
			BSPI P 5
			BSPI ONMLKJ 5
			Goto See+1
	}
}

ACTOR boltArachnotronPlasma : ArachnotronPlasma replaces ArachnotronPlasma
{
	Damage (1 * random(0, 5) + 20)
}

ACTOR boltSpiderMastermind : SpiderMastermind replaces SpiderMastermind
{
    DropItem "Clip"
    DropItem "Clip"
    DropItem "Clip"
	States
	{
		Spawn:
			SPID AB 10 A_Look
			Loop
		See:
			SPID A 3 A_Metal
			SPID ABB 3 A_Chase
			SPID C 3 A_Metal
			SPID CDD 3 A_Chase
			SPID E 3 A_Metal
			SPID EFF 3 A_Chase
			Loop
		Missile:
			SPID A 20 Bright A_FaceTarget
			SPID G 0 Bright A_PlaySound("spider/attack", CHAN_WEAPON)
			SPID G 4 Bright A_CustomBulletAttack(22.5, 0, 3,  9, "BulletPuff", 0, CBAF_NORANDOM)
			SPID H 0 Bright A_PlaySound("spider/attack", CHAN_WEAPON)
			SPID H 4 Bright A_CustomBulletAttack(22.5, 0, 3,  9, "BulletPuff", 0, CBAF_NORANDOM)
			SPID H 1 Bright A_SpidRefire
			Goto Missile+1
		Pain:
			SPID I 3
			SPID I 3 A_Pain
			Goto See
		Death:
			SPID J 20 A_Scream
			SPID K 10 A_NoBlocking
			SPID LMNOPQR 10
			SPID S 30
			SPID S -1 A_BossDeath
			Stop
	}
}

ACTOR boltCyberdemon : Cyberdemon replaces Cyberdemon
{
	-NORADIUSDMG
    DropItem "RocketAmmo"
    DropItem "RocketAmmo"
    DropItem "RocketAmmo"
	States
	{
		Spawn:
			CYBR AB 10 A_Look
			Loop
		See:
			CYBR A 3 A_Hoof
			CYBR ABBCC 3 A_Chase
			CYBR D 3 A_Metal
			CYBR D 3 A_Chase
			Loop
		Missile:
			CYBR E 6 A_FaceTarget
			CYBR F 12 A_CustomMissile("boltCyberRocket", 48, -22)
			CYBR E 12 A_FaceTarget
			CYBR F 12 A_CustomMissile("boltCyberRocket", 48, -22)
			CYBR E 12 A_FaceTarget
			CYBR F 12 A_CustomMissile("boltCyberRocket", 48, -22)
			Goto See
		Pain:
			CYBR G 10 A_Pain
			Goto See
		Death:
			CYBR H 10
			CYBR I 10 A_Scream
			CYBR JKL 10
			CYBR M 10 A_NoBlocking
			CYBR NO 10
			CYBR P 30
			CYBR P -1 A_BossDeath
			Stop
	}
}

ACTOR boltCyberRocket : Rocket
{
	Damage (2 * random(0, 10) + 80)
	States
	{
		Spawn:
			MISL A 1 Bright
			Loop
		Death:
			MISL B 8 Bright A_Explode(128, 128, 0)
			MISL C 6 Bright
			MISL D 4 Bright
			Stop
	}
}